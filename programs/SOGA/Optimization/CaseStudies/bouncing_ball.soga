array[36] H;

/* Initial state */
currH = gauss(9., 1.);   
mode = -1.;
currV = 0.;

dt = 0.08;            

for i in range(35) {
    /* Save current state */
    H[i] = currH;

    /* Continuous dynamics */

    if mode < 0 {
        /* falling down */
        temp = -9.8 * dt;
        newV = currV + temp + gauss(0., 0.1);
    } else {
        /* going up */
        temp = -9.8 * dt;

        spring = _R*currV;
        temp2 = _C*currH;
        spring = spring + temp2;
        spring = spring * dt; 
        spring = 0.14 * spring; 
        newV = currV + temp - spring + gauss(0., 0.1);
    } end if;

    currV = newV;

    temp = currV * dt;
    newH = currH + temp + gauss(0., 0.1);

    currH = newH;

    /* Mode switching */
    if mode < 0 {
        if currH <= 0. {
            mode = 1;
        } else {
            skip;
        } end if;
    } else {
        if currH > 0. {
            mode = -1;
        } else {
            skip;
        } end if;
    } end if;
} end for;

/* Save final state */
H[35] = currH;
